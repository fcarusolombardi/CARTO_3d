!Vamsi's routines for save salinity
!inimov is run once before starting the time iterations
!mkmon_dsal dumps salinity every tframe
      subroutine inimov
      use mpih
      use param
      use hdf5

      IMPLICIT none

      integer hdf_error

      integer(HID_T) :: file_grid
      integer(HID_T) :: dset_grid
      integer(HID_T) :: dspace_grid

      integer(HSIZE_T) :: dims_grid(1)

      character(70) namfile

      call h5open_f(hdf_error)

      if (myid.eq.0) then
      namfile='movie/cordin_info.h5'

!   Write the grid information normally

      call h5fcreate_f(namfile,H5F_ACC_TRUNC_F,file_grid,hdf_error)

      dims_grid(1)=n1mr
      call h5screate_simple_f(1, dims_grid, dspace_grid, hdf_error)
      call h5dcreate_f(file_grid, 'x', H5T_NATIVE_DOUBLE, &
                     dspace_grid, dset_grid, hdf_error)

      call h5dwrite_f(dset_grid, H5T_NATIVE_DOUBLE, xmr(1:n1mr), &
             dims_grid, hdf_error)


      call h5dclose_f(dset_grid, hdf_error)
      call h5sclose_f(dspace_grid, hdf_error)

      dims_grid(1)=n2mr
      call h5screate_simple_f(1, dims_grid, dspace_grid, hdf_error)

      call h5dcreate_f(file_grid, 'y', H5T_NATIVE_DOUBLE, &
                     dspace_grid, dset_grid, hdf_error)

      call h5dwrite_f(dset_grid, H5T_NATIVE_DOUBLE, ymr(1:n2mr), &
            dims_grid,hdf_error)

      call h5dclose_f(dset_grid, hdf_error)
      call h5sclose_f(dspace_grid, hdf_error)

      dims_grid(1)=n3mr
      call h5screate_simple_f(1, dims_grid, dspace_grid, hdf_error)
      call h5dcreate_f(file_grid, 'z', H5T_NATIVE_DOUBLE, &
                     dspace_grid, dset_grid, hdf_error)

      call h5dwrite_f(dset_grid, H5T_NATIVE_DOUBLE, zmr(1:n3mr), &
             dims_grid, hdf_error)


      call h5dclose_f(dset_grid, hdf_error)
      call h5sclose_f(dspace_grid, hdf_error)

      call h5fclose_f(file_grid, hdf_error)
      endif

      call h5close_f(hdf_error)

      return
      end
!***********************************************************************
      subroutine mkmov_dsal

      use local_arrays, only: dsal
      use mpi_param
      use mpih
      use hdf5
      use param

      IMPLICIT NONE

      integer hdf_error
      integer(HID_T) :: filespace
      integer(HID_T) :: slabspace
      integer(HID_T) :: memspace

      integer(HID_T) :: file_dsalv

      integer(HID_T) :: dset_dsalv

      integer(HSIZE_T) :: dims(3)

      integer(HID_T) :: file_plist
      integer(HID_T) :: slab_plist
      integer(HSIZE_T), dimension(3) :: data_count  
      integer(HSSIZE_T), dimension(3) :: data_offset 

      integer :: comm, info

      integer ndims,itime

      real(DP) :: tprfi
      character(70) filnam1,filnamxdm
      character(5) ipfi

!   Sort out MPI definitions and file names

      tprfi = 1/tframe
      itime=nint(time*tprfi)
      write(ipfi,'(i5.5)')itime

      filnam1='movie/dsal'//ipfi//'.h5'
      filnamxdm = 'movie/dsal'//ipfi//'.xmf' 

      comm = MPI_COMM_WORLD
      info = MPI_INFO_NULL

!   Set offsets and element counts

      ndims=3

      dims(1)=n1mr
      dims(2)=n2mr
      dims(3)=n3mr

      data_count(1) = n1mr
      data_count(2) = n2mr
      data_count(3) = kendr-kstartr+1

      data_offset(1) = 0
      data_offset(2) = 0
      data_offset(3) = kstartr-1 


      call h5open_f(hdf_error)

!   Set up MPI file properties

      call h5pcreate_f(H5P_FILE_ACCESS_F, file_plist, hdf_error)
      call h5pset_fapl_mpio_f(file_plist, comm, info, hdf_error)

      call h5pcreate_f(H5P_DATASET_XFER_F, slab_plist, hdf_error) 
      call h5pset_dxpl_mpio_f(slab_plist, H5FD_MPIO_COLLECTIVE_F, &
                             hdf_error)

!   Create dataspace

      call h5screate_simple_f(ndims, dims, filespace, hdf_error)

!   Create dataspace in memory

      call h5screate_simple_f(ndims, data_count, memspace, hdf_error) 

!   Open first continua file for dsal

      call h5fcreate_f(filnam1, H5F_ACC_TRUNC_F, file_dsalv, hdf_error, &
                      access_prp=file_plist)

!   Create dataset on file

      call h5dcreate_f(file_dsalv, 'S', H5T_NATIVE_DOUBLE, &
                     filespace, dset_dsalv, hdf_error)

!   Set hyperslab

      call h5dget_space_f(dset_dsalv, slabspace, hdf_error)

      call h5sselect_hyperslab_f (slabspace, H5S_SELECT_SET_F, &
                           data_offset, data_count, hdf_error)

      call h5dwrite_f(dset_dsalv, H5T_NATIVE_DOUBLE, &
        dsal(1:n1mr,1:n2mr,kstartr:kendr), dims,     & 
        hdf_error, file_space_id = slabspace, mem_space_id = memspace, & 
        xfer_prp = slab_plist)

!   Close dataset and file for dsal

      call h5dclose_f(dset_dsalv, hdf_error)
      call h5fclose_f(file_dsalv, hdf_error)

!   Close all other stuff

      call h5sclose_f(memspace, hdf_error)
      call h5sclose_f(slabspace, hdf_error)
      call h5sclose_f(filespace, hdf_error)
      call h5pclose_f(file_plist, hdf_error)
      call h5pclose_f(slab_plist, hdf_error)

      call MPI_BARRIER(MPI_COMM_WORLD,ierr)

!   Write the xdm

      if (myid.eq.0) then

      open(45,file=filnamxdm,status='unknown')
      rewind(45)
      write(45,'("<?xml version=""1.0"" ?>")')
      write(45,'("<!DOCTYPE Xdmf SYSTEM ""Xdmf.dtd"" []>")')
      write(45,'("<Xdmf Version=""2.0"">")')
      write(45,'("<Domain>")')
      write(45,'("<Grid Name=""RB Cartesian"" GridType=""Uniform"">")')
      write(45,'("<Topology TopologyType=""3DRectMesh""  &
      NumberOfElements=""",i4," ",i4," ",i4,"""/>")') n3mr,n2mr,n1mr
      write(45,'("<Geometry GeometryType=""VXVYVZ"">")')
      write(45,'("<DataItem Dimensions=""",i4,""" &
      NumberType=""Float"" Precision=""4"" Format=""HDF"">")')n1mr
      write(45,'("cordin_info.h5:/x")')
      write(45,'("</DataItem>")')
      write(45,'("<DataItem Dimensions=""",i4,""" &
      NumberType=""Float"" Precision=""4"" Format=""HDF"">")')n2mr
      write(45,'("cordin_info.h5:/y")')
      write(45,'("</DataItem>")')
      write(45,'("<DataItem Dimensions=""",i4,""" &
      NumberType=""Float"" Precision=""4"" Format=""HDF"">")')n3mr
      write(45,'("cordin_info.h5:/z")')
      write(45,'("</DataItem>")')
      write(45,'("</Geometry>")')
      write(45,'("<Attribute Name=""Salinity"" &
      AttributeType=""Scalar"" Center=""Node"">")')
      write(45,'("<DataItem Dimensions=""",i4," ",i4," ",i4,""" &
      NumberType=""Float"" Precision=""4"" Format=""HDF"">")') &
      n3mr,n2mr,n1mr
      write(45,'("dsal",i5.5,".h5:/S")') itime
      write(45,'("</DataItem>")')
      write(45,'("</Attribute>")')
      write(45,'("<Time Value=""",e12.5,""" />")') time
      write(45,'("</Grid>")')
      write(45,'("</Domain>")')
      write(45,'("</Xdmf>")')
      close(45)

      endif

      call h5close_f(hdf_error)

      return                                                          
      end                                                             

