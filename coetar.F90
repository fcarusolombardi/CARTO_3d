!************************************************************************
!                                                                       *
! ****************************** subrout coetar  ********************** *
!                                                                       *
!    this subroutine calculates the coefficients for the                *
!    integration in the radial direction with non-uniform coor. trasf.  *
!                                                                       *
!************************************************************************
      subroutine coetar
      use param
      use mpih
      implicit none
      integer :: km,kc,kp,k
      real(DP):: a33,a33m,a33p
!
!
!  ***********  coefficients for q3   for x3 differentation
!  c means centered that is at k location
!
      am3ck(1)=0.d0
      ap3ck(1)=0.d0
      ac3ck(1)=1.d0
      am3ck(n3)=0.d0
      ap3ck(n3)=0.d0
      ac3ck(n3)=1.d0
      do kc=2,n3m
        km=kc-1
        kp=kc+1
        a33=dx3q/g3rc(kc)
        a33p=1.d0/g3rm(kc)
        a33m=1.d0/g3rm(km)
        ap3ck(kc)=a33*a33p
        am3ck(kc)=a33*a33m
        ac3ck(kc)=-(ap3ck(kc)+am3ck(kc))
      enddo
!
!
!  **coefficients for q1, q2 ap3sk,am3sk,ac3sk
!   ap3ssk,am3ssk,ac3ssk, psc   for x3 differentation
!  s means staggered that is at k+1/2 location
!
      do kc=2,n3m-1
        kp=kc+1
        km=kc-1
        a33=dx3q/g3rm(kc)
        a33p= +a33/g3rc(kp)
        a33m= +a33/g3rc(kc)
        ap3sk(kc)=a33p
        am3sk(kc)=a33m
        ac3sk(kc)=-(ap3sk(kc)+am3sk(kc))
        ap3ssk(kc)=ap3sk(kc)
        am3ssk(kc)=am3sk(kc)
        ac3ssk(kc)=ac3sk(kc)
      enddo
!    
!    lower wall  bound. conditions  indicated by ubcbot
!    differemtiation of sec. derivative at 1+1/2
!    
      kc=1
      kp=kc+1
      a33=dx3q/g3rm(kc)
      a33p= +a33/g3rc(kp)
      a33m= +a33/g3rc(kc)
      ap3sk(kc)=a33p
      am3sk(kc)=0.d0
      ac3sk(kc)=-(a33p+ubcbot*a33m*2.d0)
      ap3ssk(kc)=ap3sk(kc)
      am3ssk(kc)=am3sk(kc)
      ac3ssk(kc)=-(a33p+a33m*2.d0)
!     ac3ssk(kc)=-(a33p)
!    
!    upper wall  bound. conditions  indicated by ubctop
!    differentiation of sec. derivative at n3-1/2
!    
      kc=n3m
      kp=kc+1
      a33=dx3q/g3rm(kc)
      a33p= +a33/g3rc(kp)
      a33m= +a33/g3rc(kc)
      ap3sk(kc)=0.d0
      am3sk(kc)=a33m
      ac3sk(kc)=-(a33m+ubctop*a33p*2.d0)
      ap3ssk(kc)=ap3sk(kc)
      am3ssk(kc)=am3sk(kc)
      ac3ssk(kc)=-(a33m+a33p*2.d0)
!     ac3ssk(kc)=-(a33m)
!

#ifdef DEBUG
      if(myid.eq.0)then
        open(401,file='fact/cssk.out')
        do k=1, n3m
          write(401,*) ap3ssk(k), am3ssk(k), ac3ssk(k)
        enddo
        close(401)
      endif
#endif

      return
      end


